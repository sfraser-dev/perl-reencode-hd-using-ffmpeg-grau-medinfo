@echo off

SET CONTAINER_EXTN=mp4
SET LOG_FILE="reEncodeLess44kFrames.log"
IF EXIST %LOG_FILE% (
    del %LOG_FILE%
)
:: Recursively search for media containers, call function ffmpegReEncode on each container
for /f "delims=" %%f in ('dir "*.%CONTAINER_EXTN%" /s/b') do @call:ffmpegReEncode "%%f"
goto:eof

:: Function to re-encode video using FFMPEG
:ffmpegReEncode
:: Resolving filenames
SET THE_ORIGINAL_NAME="%~f1"
SET THE_DRIVE_LETTER=%~d1
SET THE_PATH=%~p1
SET THE_FILE=%~n1
SET THE_EXTN=%~x1
SET THE_BACKUP_NAME="%THE_DRIVE_LETTER%%THE_PATH%%THE_FILE%.orig.%CONTAINER_EXTN%"
SET THE_TEMP_NAME="%THE_DRIVE_LETTER%%THE_PATH%%THE_FILE%TempFFMPEG.%CONTAINER_EXTN%"

:: FFMPEG re-encode to a temporary filename
ffmpeg -y -i %THE_ORIGINAL_NAME% -c:v libx264 -b:v 5000k -c:a libmp3lame %THE_TEMP_NAME%
:: Backup the original video
move /Y %THE_ORIGINAL_NAME% %THE_BACKUP_NAME%
:: Move the FFMPEG converted video to the original name
move /Y %THE_TEMP_NAME% %THE_ORIGINAL_NAME%
echo.Converted %THE_BACKUP_NAME% to %THE_ORIGINAL_NAME% >> %LOG_FILE%
goto:eof
